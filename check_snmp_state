#!/usr/bin/perl
# vim:et:sts=4:ai:sw=4:

# check_snmp_state - SNMP walk-capable monitoring plugin for Icinga/Nagios
# Copyright (C) 2023 Niklas.Edmundsson@hpc2n.umu.se
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU Affero General Public License as published
# by the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Affero General Public License for more details.
#
# You should have received a copy of the GNU Affero General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.

use strict;
use warnings;

use Monitoring::Plugin;
use JSON;
use List::Util qw(uniq);
use Data::Dumper;
$Data::Dumper::Sortkeys = 1;
$Data::Dumper::Indent = 1;

our $mp;

my @snmpbaseargs = ("-Oq", "-Lo");
my @snmpwalkargs = ("-Cr64");
my @rundirs = ('/run/icinga2', '/run/icinga', '/run/nagios');

# Mostly for test/debug
if($ENV{TMPDIR}) {
    @rundirs = ( $ENV{TMPDIR} );
}

# -----------------------------------

$mp = Monitoring::Plugin->new(
        usage => "Usage: %s [ -v|--verbose ] [--help]",
    );

$mp->add_arg(
    spec => 'hostname|H=s',
    help => 'Target host, aka SNMP agent',
    label => 'hostname or IP address',
    required => 1,
);

$mp->add_arg(
    spec => 'critical|c=s@',
    help => 'Critical status',
    label => 'string',
);

$mp->add_arg(
    spec => 'warning|w=s@',
    help => 'Warning status',
    label => 'string',
);

$mp->add_arg(
    spec => 'ok|O|string|s=s@',
    help => 'OK status',
    label => 'string',
);

$mp->add_arg(
    spec => 'delimiter|d=s',
    help => 'Set delimiter enabling a single critical/warning/ok argument to specify multiple strings.',
    label => 'character/string/pattern',
);

$mp->add_arg(
    spec => 'next|n',
    help => 'Use SNMP GETNEXT instead of SNMP GET',
);

$mp->add_arg(
    spec => 'walk|W',
    help => 'Do snmpwalk/snmpbulkwalk of specified tree',
);

$mp->add_arg(
    spec => 'oid|oids|o=s@',
    help => 'OIDs whose value you wish to query',
    label => 'OID1 ... OIDn',
);

$mp->add_arg(
    spec => 'entityname|E=s@',
    help => 'ENTITY-MIB::entPhysicalDescr name of entity to extract ID for and append to OID(s) of request',
    label => 'string',
);

$mp->add_arg(
    spec => 'mibdir|mibdirs|M=s@',
    help => 'MIB directory/ies',
    label => 'DIR1:...:DIRn',
);

$mp->add_arg(
    spec => 'miblist|m=s@',
    help => 'List of MIBS to be loaded',
    label => 'ALL|MIB1:...:MIBn',
);

$mp->add_arg(
    spec => 'protocol|P=s',
    help => "SNMP protocol version (default: %s)",
    label => '[1|2c|3]',
    default => '2c',
);

$mp->add_arg(
    spec => 'community|C=s',
    help => "SNMP v1/v2c community string (default: %s)",
    label => 'string',
    default => 'public',
);

$mp->add_arg(
    spec => 'context|N=s',
    help => 'SNMPv3 context',
    label => 'CONTEXT',
);

$mp->add_arg(
    spec => 'seclevel|L=s',
    help => "SNMPv3 securityLevel (default: %s)",
    label => '[noAuthNoPriv|authNoPriv|authPriv]',
    default => 'noAuthNoPriv',
);

$mp->add_arg(
    spec => 'secname|U=s',
    help => 'SNMPv3 username',
    label => 'username',
);

$mp->add_arg(
    spec => 'authproto|a=s',
    help => 'SNMPv3 auth proto',
    label => '[MD5|SHA|SHA-512|SHA-384|SHA-256|SHA-224]',
);

$mp->add_arg(
    spec => 'privproto|x=s',
    help => 'SNMPv3 priv proto',
    label => '[DES|AES]',
);

$mp->add_arg(
    spec => 'authpassword|A=s',
    help => 'SNMPv3 authentication password',
    label => 'password',
);

$mp->add_arg(
    spec => 'privpassword|privpasswd|X=s',
    help => 'SNMPv3 privacy password',
    label => 'password',
);

$mp->add_arg(
    spec => 'retries|e=i',
    help => "Number of retries to be used in the requests (default: %s)",
    default => 2,
);

$mp->add_arg(
    spec => 'snmptimeout|T=f',
    help => "SNMP timeout (default: %s)",
    default => 1,
);

$mp->getopts;

#############################################
# Basic argument mangling

my ($miblist, $mibdir);
my @oid;
my @entityname;
my $cachedir = "";
my $cachefile = "";
my %cache;
my $cache = \%cache;
my $snmpget = "snmpget";
my $snmpwalk = "snmpbulkwalk";
my @critical;
my @warning;
my @ok;
my $delimiter = "";

if($mp->opts->miblist) {
    $miblist = join(':', @{$mp->opts->miblist});
}
if($mp->opts->mibdir) {
    $mibdir = join(':', @{$mp->opts->mibdir});
}
if($mp->opts->oid) {
    foreach my $o (@{$mp->opts->oid}) {
        push @oid, split(/ /, $o);
    }
}

if($mp->opts->protocol eq "1") {
    $snmpwalk = "snmpwalk";
    @snmpwalkargs = ();
}

if($mp->opts->next) {
    $snmpget = "snmpgetnext";
}

if($mp->opts->delimiter) {
    $delimiter = $mp->opts->delimiter;
}

if($mp->opts->critical) {
    foreach my $c (@{$mp->opts->critical}) {
        if($delimiter ne "") {
            push @critical, split(/$delimiter/, $c);
        }
        else {
            push @critical, $c;
        }
    }
}

if($mp->opts->warning) {
    foreach my $w (@{$mp->opts->warning}) {
        if($delimiter ne "") {
            push @warning, split(/$delimiter/, $w);
        }
        else {
            push @warning, $w;
        }
    }
}

if($mp->opts->ok) {
    foreach my $o (@{$mp->opts->ok}) {
        if($delimiter ne "") {
            push @ok, split(/$delimiter/, $o);
        }
        else {
            push @ok, $o;
        }
    }
}

if($mp->opts->entityname) {
    foreach my $e (@{$mp->opts->entityname}) {
       push @entityname, $e;
    }
    # Figure out which dir to use for caching entPhysicalDescr info.
    foreach my $d (@rundirs) {
        if(-d $d) {
            $cachedir = $d;
            last;
        }
    }
    if(!$cachedir) {
        $mp->plugin_die("Unable to figure out which cache directory to use");
    }
    $cachedir .= '/' . lc($mp->shortname);
    if(! -d $cachedir) {
        mkdir($cachedir) or $mp->plugin_die("mkdir $cachedir: $!");
    }
    $cachefile = "$cachedir/" . $mp->opts->hostname . ".json";
    if(-s $cachefile) {
        open(my $fh, '<', $cachefile) or $mp->plugin_die("open $cachefile: $!");
        $cache = decode_json(<$fh>);

        close($fh) or $mp->plugin_die("close $cachefile: $!");
    }
}

#############################################
# Sanity-check arguments

if(@ARGV) {
    $mp->plugin_die("Leftover arguments without specifying option");
}

if(! @oid) {
    $mp->plugin_die("Must specify at least one OID to check!");
}

if($mp->opts->walk && scalar(@oid) > 1) {
    $mp->plugin_die("Can't walk more than one tree at a time.");
}

if($mp->opts->walk && $mp->opts->entityname) {
    $mp->plugin_die("Can't walk and select on ENTITY name at the same time.");
}

if($mp->opts->walk && $mp->opts->next) {
    $mp->plugin_die("Walk always implies GETNEXT.");
}

if($mp->opts->protocol !~ /^(1|2c|3)$/) {
    $mp->plugin_die("Unknown protocol: " . $mp->opts->protocol);
}

if($mp->opts->protocol eq '3' && !$mp->opts->secname) {
    $mp->plugin_die("Must give secname when using protocol v3");
}

if($mp->opts->seclevel !~ /^(noAuthNoPriv|authNoPriv|authPriv)$/) {
    $mp->plugin_die("Unknown seclevel: " . $mp->opts->seclevel);
}

if($mp->opts->seclevel ne 'noAuthNoPriv' && !$mp->opts->authpassword) {
    $mp->plugin_die("Must give authpassword when using " . $mp->opts->seclevel);
}

if($mp->opts->seclevel eq 'authPriv' && !$mp->opts->privpassword) {
    $mp->plugin_die("Must give privpassword when using " . $mp->opts->seclevel);
}

if(!@critical && !@warning && !@ok) {
    $mp->plugin_die("Must give at least one critical/warning/ok argument to define states.");
}


if($mp->opts->verbose >=2) {
    print "miblist: " , $miblist // "" , "\n";
    print "mibdir: " , $mibdir // "" , "\n";
    print "oid: " , join(" ", @oid), "\n";
    print "critical: '" , join("' '", @critical), "'\n";
    print "warning: '" , join("' '", @warning), "'\n";
    print "ok: '" , join("' '", @ok), "'\n";
    print "delimiter: '$delimiter'\n";
    print "cachefile: '$cachefile'\n";
    print "entityname: '" , join("' '", @entityname), "'\n";
}

#############################################
# Do more useful stuff

my @cmd;
my @args;

if($mp->opts->walk) {
    push @cmd, $snmpwalk;
    push @args, @snmpwalkargs;
}
else {
    push @cmd, $snmpget;
}

push @args, @snmpbaseargs;

push @args, "-r", $mp->opts->retries, "-t", $mp->opts->snmptimeout;

if($miblist) {
    push @args, "-m", $miblist;
}

if($mibdir) {
    push @args, "-M", $mibdir;
}

push @args, "-v", $mp->opts->protocol;
if($mp->opts->protocol eq "3") {
    push @args, "-u", $mp->opts->secname;
    if($mp->opts->seclevel ne 'noAuthNoPriv') {
        push @args, "-A", $mp->opts->authpassword;
    }

    if($mp->opts->seclevel eq 'authPriv') {
        push @args, "-X", $mp->opts->privpassword;
    }
    if($mp->opts->context) {
        push @args, "-n", $mp->opts->context;
    }
    if($mp->opts->authproto) {
        push @args, "-a", $mp->opts->authproto;
    }
    if($mp->opts->privproto) {
        push @args, "-x", $mp->opts->privproto;
    }
}
else {
    push @args, "-c", $mp->opts->community;
}

push @args, $mp->opts->hostname;

# Fill up the ENTITY-MIB id cache if needed
if(@entityname && ! scalar(%{$cache})) {
    my @getlist = ( $snmpwalk );
    push @getlist, @args;
    push @getlist, "ENTITY-MIB::entPhysicalDescr";
    if($mp->opts->verbose >=2) {
        print "\@getlist: ", join(" ", @getlist), "\n";
    }
    my @all;
    open(my $fh, "-|", @getlist) or $mp->plugin_die("Running $getlist[0]: $!");
    foreach my $s (<$fh>) {
        chomp $s;
        push @all, $s;
        my($o, $v) = split(/ /, $s, 2);
        $v =~ s/^"|"$//g;
        print "o: $o  v: $v\n" if($mp->opts->verbose >=2);
        if($o =~ /([0-9]+$)/) {
            $cache->{$v} = $1;
        }
    }
    if(!close($fh)) {
        my $err;
        if($? == -1) {
            $err = "failed to execute: $!";
        }
        else {
            $err = sprintf "exited with value %d", $? >> 8;
        }
        $mp->plugin_die(join("\n", "$getlist[0] $err", @all));
    }

    open(my $outfh, '>', $cachefile) or $mp->plugin_die("open $cachefile: $!");
    print $outfh encode_json($cache);
    close($outfh) or die $mp->plugin_die("close $cachefile: $!");
}

if($mp->opts->verbose >=2) {
    print "cache:\n", Dumper($cache);
}

my @get = @cmd;
push @get, @args;

my %expected;

if(@entityname) {
    # Request entity ID:s whose name matches
    my @eoid;
    foreach my $en (@entityname) {
        next unless(defined($cache->{$en}));
        my $id = $cache->{$en};
        foreach my $o (@oid) {
            my $s = "$o.$id";
            push @eoid, $s;
            $expected{$s}{desc} = $en;
            $expected{$s}{seen} = 0;
        }
    }
    if(!@eoid) {
        $mp->plugin_die("None of the specified entity names (" . join(", ", @entityname) . ") found.");
    }
    push @get, @eoid;
}
else {
    foreach my $o (@oid) {
        push @get, $o;
        if(!$mp->opts->walk) {
            $expected{$o}{seen} = 0;
        }
    }
}

if($mp->opts->verbose >=2) {
    print "expected before:\n", Dumper(\%expected);
}

if($mp->opts->verbose >=2) {
    print "\@get: ", join(" ", @get), "\n";
}

my @crits;
my @warns;
my @oks;
my @unknowns;
my @errors;

my $lastmsg;

open(my $fh, "-|", @get) or $mp->plugin_die("Running $get[0]: $!");
LINE: foreach my $s (<$fh>) {
    chomp $s;
    my($o, $v) = split(/ /, $s, 2);
    if(!defined($v)) {
        $mp->add_message(OK, "Input '$s' didn't parse.");
        push @errors, "UNKNOWN/PARSE ERROR: $s";
        next;
    }
    if($v =~ /No Such Instance|No Such Object|Unknown Object Identifier/) {
        # Ugh, the 'No Such Instance' error is only reported as a return value
        # for this OID...
        $mp->add_message(OK, "OID $o does not exist.");
        push @errors, "UNKNOWN/error: $s";
        next;
    }
    if($o !~ /^[0-9\.]$|::/) {
        # This doesn't look like an OID, so it's likely an error message.
        $mp->add_message(OK, "Input '$s' didn't parse.");
        push @errors, "UNKNOWN/PARSE ERROR: $s";
        next;
    }
    $v =~ s/^"|"$//g;
    my $so = $o;
    $so =~ s/^[^:]+:://;
    print "s: '$s' o: '$o'  so: '$so'  v: '$v'\n" if($mp->opts->verbose >=2);

    if($expected{$o} && $expected{$o}{seen}) {
        $mp->add_message(OK, "OID $o seen more than once.");
        push @errors, "UNKNOWN/error: OID $o seen more than once.";
        next;
    }

    if(!$mp->opts->walk && !$expected{$o}) {
        $mp->add_message(OK, "OID $o not expected.");
        push @errors, "UNKNOWN/error: OID $o not expected.";
        next;
    }

    $expected{$o}{seen} = 1;

    my $vo = $o;
    if($expected{$o}{desc}) {
        $lastmsg = "$expected{$o}{desc}=$v";
        $vo = "$expected{$o}{desc} ($o)";
    }
    else {
        $lastmsg = "$so=$v";
    }
    foreach my $c (@critical) {
        if($v eq $c) {
            $mp->add_message(CRITICAL, "$lastmsg.");
            push @crits, "CRITICAL: $vo $v";
            next LINE;
        }
    }
    foreach my $w (@warning) {
        if($v eq $w) {
            $mp->add_message(WARNING, "$lastmsg.");
            push @warns, "WARNING: $vo $v";
            next LINE;
        }
    }
    foreach my $s (@ok) {
        if($v eq $s) {
            push @oks, "OK: $vo $v";
            next LINE;
        }
    }
    # Treat any leftovers as UNDEFINED.
    $mp->add_message(OK, "$lastmsg/UNDEFINED.");
    push @unknowns, "UNKNOWN/UNDEFINED: $vo $v";
}

if($mp->opts->verbose >=2) {
    print "expected after:\n", Dumper(\%expected);
}

my $missing=0;
foreach my $o (sort keys %expected) {
    next if($expected{$o}{seen});

    $missing++;

    push @errors, "UNKNOWN/missing: $o" . ($expected{$o}{desc}?" ($expected{$o}{desc})":"");
}

if($missing > 0) {
    $mp->add_message(OK, "Expected " . scalar(keys %expected) . " values, but missing $missing.");
}
elsif($mp->opts->walk) {
    # At least expect one result when walking...
    my $totresults = scalar(@oks) + scalar(@warns) + scalar(@crits) + scalar(@unknowns);
    if($totresults < 1) {
        my $msg = "Expected values, only found $totresults";
        $mp->add_message(OK, "$msg.");
        push @errors, "UNKNOWN/missing: $msg";
    }
}

if(!close($fh)) {
    my $err;
    if($? == -1) {
        $err = "failed to execute: $!";
    }
    else {
        $err = sprintf "exited with value %d", $? >> 8;
    }
    $err = "$get[0] $err";
    $mp->add_message(OK, "$err.");
    push @errors, "UNKNOWN/execution: $err";
}

my $crits = scalar @crits ? sprintf("%d Critical", scalar @crits) : undef;
my $warns = scalar @warns ? sprintf("%d Warning", scalar @warns) : undef;
my $oks = scalar @oks ? sprintf("%d OK", scalar @oks) : undef;
my $unknowns = scalar @unknowns ? sprintf("%d Unknown", scalar @unknowns) : undef;
my $errors = scalar @errors ? sprintf("%d Error", scalar @errors) : undef;

my($code, $message) = $mp->check_messages;

if(scalar(@crits)+scalar(@warns)+scalar(@oks)+scalar(@unknowns) == 1) {
    # Print single-value results as short status
    $message = $lastmsg if ($lastmsg);
}
else {
    $message = join(", ", grep($_, $crits, $warns, $oks, $unknowns, $errors)) . ($message?" - $message":"");
    if($code == OK && (scalar(@unknowns)+scalar(@errors)) == 0 && scalar keys %expected) {
        my @l = map {exists($_->{desc})?$_->{desc}:()} values %expected;
        if(@l) {
            my $l = join(", ", uniq sort @l);
            $message .= " ($l)";
        }
    }
}

if($code == OK && (scalar(@unknowns)+scalar(@errors)) > 0) {
    $code = UNKNOWN;
}

$mp->plugin_exit($code, join("\n", $message, @crits, @warns, @oks, @unknowns, @errors));
